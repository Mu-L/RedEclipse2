compositeburn = [
    shader $SHADER_DEFAULT $arg1 [
        attribute vec4 vvertex, vcolor;
        attribute vec2 vtexcoord0;
        uniform mat4 hudmatrix;
        varying vec2 texcoord0;
        varying vec4 colorscale;

        void main(void)
        {
            gl_Position = hudmatrix * vvertex;
            texcoord0 = vtexcoord0;
            colorscale = vcolor;
        }
    ] [
        uniform vec4 params; // rotation length iters speed
        uniform vec4 curmillis;
        uniform vec3 rayparams; // minstep maxdist threshold
        uniform vec2 volparams; // stepsize threshold
        varying vec2 texcoord0;
        varying vec4 colorscale;

        fragdata(0) vec4 fragcolor;

        float noise(vec3 p)
        {
            vec3 pi = floor(p), pf = p - pi;
            pi.xyz = mod(pi.xyz, 69.0);
            vec3 piinc1 = step(pi, vec3(68.0)) * (pi + 1.0);
            
            vec4 pt = vec4(pi.xy, piinc1.xy) + vec2(50.0, 161.0).xyxy;
            pt *= pt;
            pt = pt.xzxz * pt.yyww;
            
            vec2 hashmod = vec2(1.0 / (635.298681 + vec2(pi.z, piinc1.z) * 48.500388));
            vec4 hashlowz = fract(pt * hashmod.xxxx), hashhighz = fract(pt * hashmod.yyyy);
            
            vec3 blend = pf * pf * pf * (pf * (pf * 6.0 - 15.0) + 10.0);
            vec4 res0 = mix(hashlowz, hashhighz, blend.z);
            vec4 blend2 = vec4(blend.xy, vec2(1.0 - blend.xy));

            return dot(res0, blend2.zxzx * blend2.wwyy);
        }

        float fnoise(vec3 p, float time)
        {
            vec3 offset = vec3(0.0, 0.5 * time, 0.0);
            p = (p + offset) * 3.0;
            
            float f = 0.0;
            f += 0.50000 * noise(p); p *= 2.0;
            f += 0.25000 * noise(p); p *= 2.0;
            f += 0.12500 * noise(p); p *= 2.0;
            f += 0.06250 * noise(p); p *= 2.0;
            f += 0.03125 * noise(p);
            return f;
        }

        float model(vec3 p, float speed)
        {
            vec3 q = p - vec3(0.0, 0.1, 0.0);
            float sphere = length(q) - params.y;
            
            return (sphere + fnoise(q * 1.5, speed) * 0.4) * 0.8;
        }

        float raymarch(vec3 ro, vec3 rd, float speed)
        {
            float dist = 0.0;
            
            for(int i = 0; i < params.z; i++)
            {
                vec3 pos = ro + rd * dist;
                float m = model(pos, speed);
                
                if(m < rayparams.z) return dist;
                if(dist > rayparams.y) break;
                
                dist += max(m * 0.7, rayparams.x);
            }
            return -1.0;
        }

        float saturation(float bri, float adjustment)
        {
            vec3 brightness = vec3(bri), intensity = vec3(dot(brightness, vec3(0.75))),
                 mixer = mix(intensity, brightness, adjustment);
            return mixer.r;
        }

        vec2 volume(vec3 p, vec3 rd, vec3 ld, vec2 sp, float speed)
        {
            float trans = 1.0, td = 0.0, dif = 0.0, emit = 0.0;
            bool found = false;
            vec3 ldscaled = 0.1 * ld;
            
            for(int i = 0; i < params.z; i++)
            {
                float m = model(p, speed);
                p += rd * volparams.x;

                float dens = 1.0 - smoothstep(0.0, 0.35, -m);
                if(dens <= 0.0) continue;
                
                td += dens;
                trans *= dens;

                if(td > 1.0 && !found)
                {
                    found = true;
                    td = 1.0;
                    vec3 lightpos = p - ldscaled;
                    dif = clamp(1.0 - model(lightpos, speed), 0.0, 1.0);
                    emit = pow(smoothstep(-0.3, 0.0, m), 4.0);
                }
                
                if(trans < volparams.y) break;
            }
            
            trans = smoothstep(0.0, 1.0, 1.0 - pow(td / params.z, 4.5));

            float sat = clamp(saturation(((emit * trans) * 0.8 + 0.2) * 0.2, pow(trans, 0.5) * 0.4) * 5.0 - 0.4, 0.0, 1.0);
            @(if (>= (strstr $arg1 "fire") 0) [result [
                return vec2(mix(0.0, sat, trans), clamp(fnoise(p + trans + sat, curmillis.x) * 3.0 - 1.0, 0.0, 1.0) * trans);
            ]] [result [
                return vec2(mix(0.0, sat, trans), trans);
            ]])
        }

        void main(void)
        {
            vec2 co = texcoord0 * 2.0 - 1.0;
            
            if(dot(co, co) > 2.25) discard;
            
            float speed = curmillis.x * params.w, cosrot = cos(params.x), sinrot = sin(params.x);
            vec3 ta = vec3(0.0, 0.1, 0.0);
            vec3 ro = vec3(cosrot, 1.0, sinrot);
            
            vec3 w = normalize(ta - ro);
            vec3 u = normalize(cross(w, vec3(0.0, 1.0, 0.0)));
            vec3 v = normalize(cross(u, w));
            vec3 rd = normalize(mat3(u, v, w) * vec3(co.xy, 1.0));
            
            float dist = raymarch(ro, rd, speed);
            if(dist <= 0.0) discard;
            
            vec3 pos = ro + rd * dist;
            fragcolor.rg = volume(pos, rd, vec3(-1.0, 1.0, 0.0), co, speed) * colorscale.ra;
        }
    ]
]

compositeburn "compositeburnfire"
compositeburn "compositeburnsmoke"

newui fire $SURFACE_COMPOSITE [
    @(localargs params [0.0 1.0 5.0 2.0])
    @(localargs rayparams [0.02 2.0 0.008])
    @(localargs volparams [0.035 0.005])
    execid uiarg1
    uivlist 0 [
        @(localdef k 0)
        loop i 2 [
            uihlist 0 [
                loop j 2 [
                    uirender compositeburnfire 0.5 0.5 [
                        uirenderparam params (+f $params1 (*f $k 45)) $params2 $params3 $params4
                        uirenderparam rayparams $rayparams1 $rayparams2 $rayparams3
                        uirenderparam volparams $volparams1 $volparams2
                    ]
                    k = (+ $k 1)
                ]
            ]
        ]
    ]
]

newui smoke $SURFACE_COMPOSITE [
    @(localargs params [0.0 1.0 4.0 1.0])
    @(localargs rayparams [0.02 2.0 0.008])
    @(localargs volparams [0.035 0.005])
    execid uiarg1
    uivlist 0 [
        @(localdef k 0)
        loop i 2 [
            uihlist 0 [
                loop j 2 [
                    uirender compositeburnsmoke 0.5 0.5 [
                        uirenderparam params (+f $params1 (*f $k 45)) $params2 $params3 $params4
                        uirenderparam rayparams $rayparams1 $rayparams2 $rayparams3
                        uirenderparam volparams $volparams1 $volparams2
                        uicolourblend 0.5
                    ]
                    k = (+ $k 1)
                ]
            ]
        ]
    ]
]
